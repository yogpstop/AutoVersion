plugins {
    id 'java-gradle-plugin'
    id 'signing'
    id 'maven-publish'
}

tasks.withType(AbstractCompile)*.options*.encoding = 'UTF-8'
tasks.withType(GroovyCompile)*.groovyOptions*.encoding = 'UTF-8'
tasks.withType(Javadoc)*.options*.encoding = 'UTF-8'

group = 'com.yogpc'
version = '0.0.4'

repositories {
    google()
    mavenCentral()
}
dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r'
}

java {
    withJavadocJar()
    withSourcesJar()
}
publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = 'Auto Version'
                description = 'Gradle plugin that set version of Android project by date and Git commit status automatically.'
                url = 'https://github.com/yogpstop/AutoVersion'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://raw.githubusercontent.com/yogpstop/AutoVersion/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'yogpstop'
                        name = 'yogpstop'
                        email = 'git@yogpc.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/yogpstop/AutoVersion.git'
                    developerConnection = 'scm:git:https://github.com/yogpstop/AutoVersion.git'
                    url = 'https://github.com/yogpstop/AutoVersion'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
        }
    }
}
signing {
    sign publishing.publications.pluginMaven
}
